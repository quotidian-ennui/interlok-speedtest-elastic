ext {
  interlokParentGradle = "https://raw.githubusercontent.com/adaptris/interlok-build-parent/develop/v4/build.gradle"
  interlokSnapshotMetadata = "https://nexus.adaptris.net/nexus/content/repositories/snapshots/com/adaptris/interlok-core/maven-metadata.xml"
  interlokReleaseMetadata = "https://nexus.adaptris.net/nexus/content/repositories/releases/com/adaptris/interlok-core/maven-metadata.xml"
  interlokVersioning = [
    latestRelease: { ->
      def mavenMetadata=new XmlSlurper().parse(interlokReleaseMetadata)
      return mavenMetadata.versioning.latest.text()
    },
    latestSnapshot: {->
      def mavenMetadata=new XmlSlurper().parse(interlokSnapshotMetadata)
      return mavenMetadata.versioning.latest.text()
    }
  ]
  javaProvider= [
    vendor: { ->
      return System.getProperty("java.vendor");
    },

    isGraalVM: { ->
      return javaProvider.vendor().toLowerCase().contains("graalvm");
    }
  ]
  // interlokVersion = project.findProperty('interlokVersion') ?: interlokVersioning.latestSnapshot()
  interlokVersion = "4.5-SNAPSHOT"
  graalvmVersion="22.0.0.2"
}

allprojects {
  apply from: "${interlokParentGradle}"
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, "seconds"
  exclude group: 'javax', module: 'javaee-api'
  exclude group: 'org.apache.geronimo.specs', module: 'geronimo-jms_2.0_spec'
  // exclude group: 'org.hibernate.javax.persistence', module: 'hibernate-jpa-2.1-api'
}

dependencies {
  // def interlokDeps = [ "core", "boot", "logging", "common", "core-apt",
  //               "varsub", "csv", "json", "elastic-rest", "apache-http",
  //               "expressions", "rest-health-check", "profiler"
  //               ] as String[]

  // interlokDeps.each {
  //   add("interlokRuntime",  [group: "com.adaptris", name: "interlok-${it}", version: interlokVersion, changing: true])
  //   // add("interlokJavadocs", [group: "com.adaptris", name: "interlok-${it}", version: interlokVersion, changing: true, classifier: "javadoc", transitive: false])
  // }

  // Explicit for dependabot.
  interlokRuntime ("com.adaptris:interlok-csv:$interlokVersion") { changing=true }
  interlokRuntime ("com.adaptris:interlok-json:$interlokVersion") { changing=true }
  interlokRuntime ("com.adaptris:interlok-elastic-rest:$interlokVersion") { changing=true }
  interlokRuntime ("com.adaptris:interlok-apache-http:$interlokVersion") { changing=true }
  interlokRuntime ("com.adaptris:interlok-expressions:$interlokVersion") { changing=true }
  interlokRuntime ("com.adaptris:interlok-rest-health-check:$interlokVersion") { changing=true }
  interlokRuntime ("com.adaptris:interlok-profiler:$interlokVersion") { changing=true }

  interlokRuntime ("jakarta.jms:jakarta.jms-api:2.0.3")
  // interlokRuntime ("jakarta.persistence:jakarta.persistence-api:2.2.3")

  interlokRuntime (platform("com.fasterxml.jackson:jackson-bom:2.13.2.20220328"))
  interlokRuntime ("com.fasterxml.jackson.dataformat:jackson-dataformat-cbor")
  interlokRuntime ("com.fasterxml.jackson.core:jackson-databind")
  interlokRuntime ("co.elastic.logging:log4j2-ecs-layout:1.3.2")
  if (!javaProvider.isGraalVM()) {
    // We assume that in this instance you're building with the
    // JVM you expect to run with, might be potentially incorrect.
    interlokRuntime ("org.graalvm.sdk:graal-sdk:$graalvmVersion")
    interlokRuntime ("org.graalvm.js:js:$graalvmVersion")
    interlokRuntime ("org.graalvm.js:js-scriptengine:$graalvmVersion")
  }
}

task deleteGeneratedFiles(type: Delete) {
  delete "logs"
}

clean.dependsOn deleteGeneratedFiles

